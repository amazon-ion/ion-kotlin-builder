plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.21"

    // benchmark
    id "me.champeau.gradle.jmh" version "0.4.8"

    // Publishing
    id "org.jetbrains.dokka" version "0.9.18"
    id "maven-publish"
    id "signing"
}

group = "com.amazon.ion"
version = "1.1.0-SNAPSHOT"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "com.amazon.ion:ion-java:[1.4.0,)"   // always want to use the latest ion-java
    implementation "org.jetbrains.kotlin:kotlin-stdlib" // version from plugin

    // test dependencies
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.3.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.1"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
}

// Benchmark

jmh {
    fork = 1
    benchmarkMode = ["thrpt"]
    failOnError = true

    // warmup
    warmupIterations = 5
    warmup = '5s'

    // iterations
    iterations = 10
    timeOnIteration = '5s'
}

// builds the JMH code as part of the regular build to avoid to make sure it doesn't fall behind
build.finalizedBy(jmhClasses)

dokka {
    outputFormat = "html"
    outputDirectory = "$buildDir/javadoc"
}

// Publishing
task sourcesJar(type: Jar) {
    from "src"
    classifier = "sources"
}
task javadocJar(type: Jar) {
    from dokka
    classifier = "javadoc"
}
publishing {
    publications {
        maven(MavenPublication) {
            artifactId = "ion-kotlin-builder"

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "Ion Kotlin Builder"
                packaging = "jar"
                url = "https://github.com/amzn/ion-kotlin-builder"
                description = "Kotlin Type-Safe Builders for writing Ion documents"
                scm {
                    connection = "scm:git@github.com:amzn/ion-kotlin-builder.git"
                    developerConnection = "scm:git@github.com:amzn/ion-kotlin-builder.git"
                    url = "git@github.com:amzn/ion-kotlin-builder.git"
                }
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        name = "Amazon Ion Team"
                        email = "ion-team@amazon.com"
                        organization = "Amazon"
                        organizationUrl = "https://github.com/amzn"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://aws.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}
